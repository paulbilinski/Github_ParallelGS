model{
#loop over mothers, define the mother random effects
for ( m in 1:n.mothers) {
	z.motherStCS[m] ~ dnorm( 0 , tau.motherStCS ) # tau is a precision 1/variance
	z.motherGS[m] ~ dnorm( 0 , tau.motherGS ) # tau is a precision 1/variance
	#mother random effects for stomatal cell size and genome size respectively
}
#loop over individuals, define the individual random effects, define model for GS on stomatal CS, StCS
for ( i in 1:n.indiv) {
	z.indiv[i] ~ dnorm( 0 , tau.indiv ) # same for individuals; this is an individual random effect on StCS

	mu.GS[i] <- Mu.GS + z.motherGS[mother.index[i]] #Predicted genome size for individual i
	#additive model involving a grand mean and a maternal effect

	GS[i] ~ dnorm( mu.GS[i], tau.GS )
	#defines the distribution for the observed GS

	mu.StCS[i] <- Mu.StCS + z.indiv[i] + z.motherStCS[mother.index[i]] + b.GS * GS[i]
	#additive model for mean Stcell size, involving grand mean, individual effect, maternal effect, and regression on genome size

	#loop over cells within individuals
	for ( c in 1:n.cells) {
		#model for mean cell size
		stoma[i,c] ~ dnorm( mu.StCS[i], tau.StCS)
		#have to make sure to code the bulk seed as each with a different integer
	}
}
#Priors scheme for half-Cauchy priors for standard deviation
sd.motherStCS ~ dt(0, 1, 1) T(0,)
sd.motherGS ~ dt(0, 1, 1) T(0,)
sd.GS ~ dt(0, 1, 1) T(0,)
sd.indiv ~ dt(0, 1, 1) T(0,)
sd.StCS ~ dt(0, 1, 1) T(0,)


#deterministically call the tau based on the SD
tau.motherStCS <- 1/(sd.motherStCS * sd.motherStCS)
tau.motherGS <- 1/(sd.motherGS * sd.motherGS)
tau.GS <- 1/(sd.GS * sd.GS)
tau.indiv <- 1/(sd.indiv * sd.indiv)
tau.StCS <- 1/(sd.StCS * sd.StCS)

#Leaving the prior for grand mean cell size, and grand mean genome size
Mu.StCS ~ dnorm( 0 , 1e-4)
Mu.GS ~ dnorm(0 , 1e-4)

#Priors for coefficients
b.GS ~ dnorm( 0 , 1e-4)

}