#redo whole revision to reflect the named terms (eg, beta tao gamma) as will be written in the paper
#microns everywhere

#priors: stomatal cell size (length) ~30um
#daily growth = 3.5cm #make sure to ask a physiologist, not from my stuff, 
#mean genome size 6 pg #goes into mother because we dont have random effect from individual
#priors^^^

model{
#loop over mothers, define the mother random effects
for ( m in 1:n.mothers) {
	z.motherStCS[m] ~ dnorm( 0 , tau.motherStCS ) # tau is a precision 1/variance
	z.motherGS[m] ~ dnorm( log(6) , tau.motherGS ) # tau is a precision 1/variance
	z.motherLL[m] ~ dnorm( 0 , tau.motherLL ) # tau is a precision 1/variance
	z.motherLE[m] ~ dnorm( 0 , tau.motherLE ) # tau is a precision 1/variance
	#mother random effects for stomatal cell size and genome size respectively
}
#loop over individuals, define the individual random effects, define model for GS on stomatal CS, StCS
for ( i in 1:n.indiv) {
	z.indivCS[i] ~ dnorm( log(30) , tau.indivCS ) # same for individuals; this is an individual random effect on StCS
	z.indivLL[i] ~ dnorm( log(3.5) , tau.indivLL )
	z.indivLE[i] ~ dnorm( 0 , tau.indivLE )
	#for consistency, we want to update the individual effects to contain the prior mean info
	
#	mu.GS[i] <- Mu.GS + z.motherGS[mother.index[i]] #Predicted genome size for individual i
	mu.GS[i] <- z.motherGS[mother.index[i]] #Predicted genome size for individual i #mean falls into mother effect, just like everywhere else
	#additive model involving a grand mean and a maternal effect

	GS[i] ~ dnorm( mu.GS[i], tau.GS )
	#defines the distribution for the observed GS

#	mu.StCS[i] <- Mu.StCS + z.indivCS[i] + z.motherStCS[mother.index[i]] + b.GS * GS[i]
	mu.StCS[i] <- z.indivCS[i] + z.motherStCS[mother.index[i]] + b.GS * GS[i] #Mu.StCS falls into individual effect, not mother like for GS
	#additive model for mean Stcell size, involving grand mean, individual effect, maternal effect, and regression on genome size

	#loop over cells within individuals
	for ( c in 1:n.cells[i]) {
		#model for mean cell size
		stoma[i,c] ~ dnorm( mu.StCS[i], tau.StCS)
		#DONE;have to make sure to code the bulk seed as each with a different integer
	}

#	mu.LL[i] <- Mu.LL + z.indivLL[i] + z.motherLL[mother.index[i]] + b.LL * GS[i]
	mu.LL[i] <- z.indivLL[i] + z.motherLL[mother.index[i]] + b.LL * GS[i]
	#additive model for mean leaf length, involving only the non time dependent components
	
	#loop over time points for leaf length measurement within individuals
	for ( t in 1:n.times) {
		#model for leaf length in individual i at time t
		mu.LL.time[i,t] <- mu.LL[i] + ( Mu.LE + z.indivLE[i] + z.motherLE[mother.index[i]]) * log(t)
		leaflength[i,t] ~ dnorm( mu.LL.time[i,t], tau.LL) # the error terms are implicit within the variance at the lowest level of the model
	}
}

#Priors scheme for half-Cauchy priors for standard deviation
sd.motherStCS ~ dt(0, 1, 1) T(0,)
sd.motherGS ~ dt(0, 1, 1) T(0,)
sd.motherLL ~ dt(0, 1, 1) T(0,)
sd.motherLE ~ dt(0, 1, 1) T(0,)
sd.GS ~ dt(0, 1, 1) T(0,)
sd.indivCS ~ dt(0, 1, 1) T(0,) #individual level effect on cell sizes... compare to...
sd.indivLL ~ dt(0, 1, 1) T(0,)
sd.indivLE ~ dt(0, 1, 1) T(0,)
sd.StCS ~ dt(0, 1, 1) T(0,) #error sd for replicates. so not the same as indivCS
sd.LL ~ dt(0, 1, 1) T(0,) #error sd for replicates. so not the same as indivLL

#deterministically call the tau based on the SD
tau.motherStCS <- 1/(sd.motherStCS * sd.motherStCS)
tau.motherGS <- 1/(sd.motherGS * sd.motherGS)
tau.motherLL <- 1/(sd.motherLL * sd.motherLL)
tau.motherLE <- 1/(sd.motherLE * sd.motherLE)
tau.GS <- 1/(sd.GS * sd.GS)
tau.indivCS <- 1/(sd.indivCS * sd.indivCS)
tau.indivLL <- 1/(sd.indivLL * sd.indivLL)
tau.indivLE <- 1/(sd.indivLE * sd.indivLE)
tau.StCS <- 1/(sd.StCS * sd.StCS)
tau.LL <- 1/(sd.LL * sd.LL)

#Leaving the prior for grand mean cell size, and grand mean genome size
#Mu.StCS ~ dnorm( 0 , 1)#deleted, incorporated into individual term
#Mu.GS ~ dnorm(0 , 0.04)#deleted, incorporated into mother term
#Mu.LL ~ dnorm(log(3.5), 1)#
Mu.LE ~ dnorm(1, 0.04) #prior at 1 because we expect linear growth, tracker will show this

#Priors for coefficients
b.GS ~ dnorm( 0 , 0.04)#small variance because we expect small effects
b.LL ~ dnorm( 0 , 0.04)
GS.contrast <- b.LL - b.GS

}