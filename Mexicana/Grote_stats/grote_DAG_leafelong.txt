model{
#loop over mothers, define the mother random effects
for ( m in 1:n.mothers) {
	z.motherStCS[m] ~ dnorm( 0 , tau.motherStCS ) # tau is a precision 1/variance
	z.motherGS[m] ~ dnorm( 0 , tau.motherGS ) # tau is a precision 1/variance
	z.motherLL[m] ~ dnorm( 0 , tau.motherLL ) # tau is a precision 1/variance
	z.motherLE[m] ~ dnorm( 0 , tau.motherLE ) # tau is a precision 1/variance
	#mother random effects for stomatal cell size and genome size respectively
}
#loop over individuals, define the individual random effects, define model for GS on stomatal CS, StCS
for ( i in 1:n.indiv) {
	z.indivCS[i] ~ dnorm( 0 , tau.indivCS ) # same for individuals; this is an individual random effect on StCS
	z.indivLL[i] ~ dnorm( 0 , tau.indivLL )
	z.indivLE[i] ~ dnorm( 0 , tau.indivLE )
	
	mu.GS[i] <- Mu.GS + z.motherGS[mother.index[i]] #Predicted genome size for individual i
	#additive model involving a grand mean and a maternal effect

	GS[i] ~ dnorm( mu.GS[i], tau.GS )
	#defines the distribution for the observed GS

	mu.StCS[i] <- Mu.StCS + z.indivCS[i] + z.motherStCS[mother.index[i]] + b.GS * GS[i]
	#additive model for mean Stcell size, involving grand mean, individual effect, maternal effect, and regression on genome size

	#loop over cells within individuals
	for ( c in 1:n.cells[i]) {
		#model for mean cell size
		stoma[i,c] ~ dnorm( mu.StCS[i], tau.StCS)
		#have to make sure to code the bulk seed as each with a different integer
	}

	mu.LL[i] <- Mu.LL + z.indivLL[i] + z.motherLL[mother.index[i]] + b.LL * GS[i]
	#additive model for mean leaf length, involving only the non time dependent components
	
	#loop over time points for leaf length measurement within individuals
	for ( t in 1:n.times) {
		#model for leaf length in individual i at time t
		mu.LL.time[i,t] <- mu.LL[i] + ( Mu.LE + z.indivLE[i] + z.motherLE[mother.index[i]]) * log(t)
		leaflength[i,t] ~ dnorm( mu.LL.time[i,t], tau.LL) # the error terms are implicit within the variance at the lowest level of the model
	}
}

#Priors scheme for half-Cauchy priors for standard deviation
sd.motherStCS ~ dt(0, 1, 1) T(0,)
sd.motherGS ~ dt(0, 1, 1) T(0,)
sd.motherLL ~ dt(0, 1, 1) T(0,)
sd.motherLE ~ dt(0, 1, 1) T(0,)
sd.GS ~ dt(0, 1, 1) T(0,)
sd.indivCS ~ dt(0, 1, 1) T(0,) #individual level effect on cell sizes... compare to...
sd.indivLL ~ dt(0, 1, 1) T(0,)
sd.indivLE ~ dt(0, 1, 1) T(0,)
sd.StCS ~ dt(0, 1, 1) T(0,) #error sd for replicates. so not the same as indivCS
sd.LL ~ dt(0, 1, 1) T(0,) #error sd for replicates. so not the same as indivLL

#deterministically call the tau based on the SD
tau.motherStCS <- 1/(sd.motherStCS * sd.motherStCS)
tau.motherGS <- 1/(sd.motherGS * sd.motherGS)
tau.motherLL <- 1/(sd.motherLL * sd.motherLL)
tau.motherLE <- 1/(sd.motherLE * sd.motherLE)
tau.GS <- 1/(sd.GS * sd.GS)
tau.indivCS <- 1/(sd.indivCS * sd.indivCS)
tau.indivLL <- 1/(sd.indivLL * sd.indivLL)
tau.indivLE <- 1/(sd.indivLE * sd.indivLE)
tau.StCS <- 1/(sd.StCS * sd.StCS)
tau.LL <- 1/(sd.LL * sd.LL)

#Leaving the prior for grand mean cell size, and grand mean genome size
Mu.StCS ~ dnorm( 0 , 0.04)
Mu.GS ~ dnorm(0 , 0.04)
Mu.LL ~ dnorm(0, 0.04)
Mu.LE ~ dnorm(0, 0.04) #WILL NEED TRACKER HOPE TO SEE =1

#Priors for coefficients
b.GS ~ dnorm( 0 , 0.04)
b.LL ~ dnorm( 0 , 0.04)
GS.contrast <- b.LL - b.GS

}